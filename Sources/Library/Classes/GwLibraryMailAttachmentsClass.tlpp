#include "TOTVS.ch"

using namespace Gworks.Library.Functions

namespace Gworks.Library.Classes

/*/{Protheus.doc} GwMailAttachments
Função para envio de e-mail com suporte a anexo.
@type class
@version 12.1.2310
@author Gwork - Giovani
@since 29/04/2025
/*/
Class GwMailAttachments

    Public Data cDirectory as character default ""
    Public Data lReserved  as logical default .F.

    Public Data cEmailTO as character default ""
    Public Data cEmailCC as character default ""
    Public Data cEmailCCO as character default ""
    Public Data cSubject as character default ""
    Public Data cBody as character default ""
    Public Data cFileType as character default "*.pdf, *.csv"
    Public Data lSplitMail as logical default .F.
    Public Data lError as logical default .F.
    Public Data cError as character default ""

    Public Method New() Constructor
    Public Method ReserveAttachDir()
    Public Method ReleaseAttachDir()
    Public Method GetNameAttachDir()
    Public Method AttachFiles()
    Public Method SendMail()

EndClass

/*/{Protheus.doc} GwMailAttachments::New
Método construtor da classe.
@type method
@version 12.1.2310
@author Gworks - Giovani
@since 29/04/2025
@return object, Instância da classe.
/*/
Method New() Class GwMailAttachments

    Self:cDirectory := ""
    Self:lReserved  := .F.

Return Self

/*/{Protheus.doc} GwMailAttachments::ReserveAttachDir
Método para preparar e reservar o diretório de anexos.
@type method
@version 12.1.2310
@author Gworks - Giovani
@since 29/04/2025
@param cAttachDirectory, character, Diretório de anexos.
@param lEraseFiles, logical, Se .T. (true), apagará os arquivos existentes.
@return variant, Retorna .T. (true) para indicar sucesso na operação e .F. (false) caso contrário.
/*/
Method ReserveAttachDir( cAttachDirectory as character, lEraseFiles as logical ) Class GwMailAttachments

    Local nInterval      := 5000 as numeric // intervalo entre tentativas
    Local nAttempt       := 0    as numeric // nro. tentativas atual
    Local nTotalAttempts := 10   as numeric // nro. tentativas
    Local lContinue      := .F.  as logical
    Local lResult        := .F.  as logical

    Default cAttachDirectory := "\attachments\"
    Default lEraseFiles      := .T.

    // Tenta criar o semáforo para continuar com o processamento
    // Obs.: É necessário empregar semáforo pois a pasta "attachments"
    //       pode ser utilizada em diferentes processos/threads concorrentes.
    while lContinue .Or. ( !lContinue .And. nAttempt < nTotalAttempts )

        nAttempt++

        if ::lReserved .Or. LockByName(cAttachDirectory, .F./*lEmpresa*/, .F./*lFilial*/)

            lContinue := .T.

            Exit

        endif

        lContinue := .F.

        Sleep(nInterval)

    enddo

    if lContinue

        ::lReserved := .T.
        ::cDirectory := cAttachDirectory

        // Cria o Diretório
        if !ExistDir(cAttachDirectory,,.F.)
            MakeDir(cAttachDirectory,,.F.)
        endif

        // Apaga qualquer arquivo existente no diretório
        if lEraseFiles
            aEval( Directory(cAttachDirectory+"*.*",,,.F.), { |aFile| FErase(cAttachDirectory+aFile[1]) })
        endif

        lResult := .T.

    else

        lResult := .F.

    endif

Return lResult

/*/{Protheus.doc} GwMailAttachments::ReleaseAttachDir
Método para liberar a reserva do diretório de anexos.
@type method
@version 12.1.2310
@author Gworks - Giovani
@since 29/04/2025
@param lEraseFiles, logical, Se .T. (true), apagará os arquivos existentes.
/*/
Method ReleaseAttachDir( lEraseFiles as logical ) Class GwMailAttachments

    Default lEraseFiles := .T.

    // Apaga qualquer arquivo existente no diretório
    if lEraseFiles
        aEval( Directory(::cDirectory+"*.*",,,.F.), { |aFile| FErase(::cDirectory+aFile[1]) })
    endif

    // Libera o semáforo para o processamento
	if ::lReserved
        UnLockByName(::cDirectory, .F./*lEmpresa*/ , .F./*lFilial*/)
    endif

    ::lReserved := .F.
    ::cDirectory := ""

Return

/*/{Protheus.doc} GwMailAttachments::GetNameAttachDir
Método para retornar o diretório de anexos.
@type method
@version 12.1.2310
@author giovani
@since 29/04/2025
@return character, Diretório de anexos.
/*/
Method GetNameAttachDir() Class GwMailAttachments
Return ::cDirectory

/*/{Protheus.doc} GwMailAttachments::AttachFiles
Método responsábel por copiar os arquivos da máquina local para o servidor.
@type method
@version 12.1.2310
@author Gworks - Giovani
@since 29/04/2025
@param cLocalDir, character, Diretório Local
@param lEraseLocal, logical, Se .T. (true), apagará os arquivos existentes.
@return logical, Retorna .T. (true) para indicar sucesso na operação e .F. (false) caso contrário.
/*/
Method AttachFiles( cLocalDir as character, lEraseLocal as logical ) Class GwMailAttachments

    Local nI        := 0   as numeric
    Local aFiles    := {}  as array
    Local cFile     := ""  as character

    Default cLocalDir   := ""  // diretório local, origem dos arquivos a serem copiados
    Default lEraseLocal := .T. // apaga arquivos após a cópia para o servidor

    if Empty(cLocalDir)
        UserException("GwMailAttachments:AttachFiles - Argumentos inválidos ou não informados!")
    endif

    if !::lReserved .and. !::ReserveAttachDir(,.F.)
        UserException("GwMailAttachments:AttachFiles - Falha ao reservar o diretório de anexos!")
    endif

    do case
        case at("\",cLocalDir) > 0 .and. Right(cLocalDir,1) != "\" ; cLocalDir += "\" // Windows
        case at("/",cLocalDir) > 0 .and. Right(cLocalDir,1) != "/" ; cLocalDir += "/" // Linux
    endcase

    aFiles := Directory(cLocalDir+"*.*",,,.F.)

    // Move os arquivos para o Servidor
    for nI:=1 to Len(aFiles)

        cFile := cLocalDir+aFiles[nI,1]

        if CpyT2S(cFile, ::cDirectory, .F.) // se copiou...

            if lEraseLocal // ...e deve-se remover os arquivos de origem...
                FErase(cFile,,.F.) // ...então apaga-os do diretório local
            endif

        else
            UserException("GwMailAttachments:AttachFiles - Falha ao copiar o arquivo " + cFile + " para o diretório " + ::cDirectory +;
                ". Ferifique se o arquivo está em uso e ente novamente.")
        endif

    next

Return .T.

/*/{Protheus.doc} GwMailAttachments::SendMail
Méotodo responsável por enviar o respectivo e-mail com anexos.
@type method
@version 12.1.2310
@author Gworks - Giovani
@since 29/04/2025
@param _cEmailTO, character, Endereço de e-mail do destinatário.
@param _cEmailCC, character, Endereço de e-mail do destinatário em cópia.
@param _cEmailCCO, character, Endereço de e-mail do destinatário em cópia oculta.
@param _cSubject, character, Assunto do e-mail.
@param _cBody, character, Corpo do e-mail.
@param _cFileType, character, Tipos de arquivos a serem enviados, ex.: *.pdf, *.txt
@param _lSplitMail, logical, Se .T. (true), enviará um e-mail para cada destinatário separadamente.
@param _cRetError, character, Retorna a mensagem de erro ao tentar enviar o e-mail, se houver.
@return logical, Retorna .T. (true) para indicar sucesso na operação e .F. (false) caso contrário.
/*/
Method SendMail( _cEmailTO   as character,;
                 _cEmailCC   as character,;
                 _cEmailCCO  as character,;
                 _cSubject   as character,;
                 _cBody      as character,;
                 _cFileType  as character,;
                 _lSplitMail as logical,;
                 _cRetError  as character ) Class GwMailAttachments

    Local aDirFiles   := {}  as array
    Local aFileType   := {}  as array
    Local aFiles      := {}  as array
    Local aEmail      := {}  as array
    Local cFileType   := ""  as character
    Local cFileName   := ""  as character
    Local cTO         := ""  as character
    Local nI          := 0   as numeric
    Local nJ          := 0   as numeric
    Local lResult     := .T. as logical

    Default _cEmailTO   := nil
    Default _cEmailCC   := nil
    Default _cSubject   := nil
    Default _cBody      := nil
    Default _cFileType  := nil
    Default _lSplitMail := nil
    Default _cRetError  := ""

    ::cEmailTO   := iif( !empty(_cEmailTO)  , _cEmailTO  , ::cEmailTO   )
    ::cEmailCC   := iif( !empty(_cEmailCC)  , _cEmailCC  , ::cEmailCC   )
    ::cSubject   := iif( !empty(_cSubject)  , _cSubject  , ::cSubject   )
    ::cBody      := iif( !empty(_cBody)     , _cBody     , ::cBody      )
    ::cFileType  := iif( !empty(_cFileType) , _cFileType , ::cFileType  )
    ::lSplitMail := iif( !empty(_lSplitMail), _lSplitMail, ::lSplitMail )

    if  Empty(::cEmailTO) .Or.;
        Empty(::cSubject) .Or.;
        Empty(::cBody)

        UserException("GwMailAttachments:SendMail - Argumentos inválidos ou não informados!")

    endif

    ::cEmailTO  := Replace(::cEmailTO ,' ','')
    ::cEmailCC  := Replace(::cEmailCC ,' ','')
    ::cEmailCCO := Replace(::cEmailCCO,' ','')

    // Ontem a lista de e-mails caso seja necessário
    // enviar um e-mail para cada destinatário separadamente
    if ::lSplitMail
        if Empty(aEmail) .And. At(',',::cEmailTO) > 0
            aEmail := StrTokArr(::cEmailTO,',') // suporte a e-mails separados por ','
        endif
        if Empty(aEmail) .And. At(';',::cEmailTO) > 0
            aEmail := StrTokArr(::cEmailTO,';') // suporte a e-mails separados por ';'
        endif
    endif
    if Empty(aEmail)
        aAdd(aEmail, ::cEmailTO)
    endif
    ::cEmailTO := ""

    // Obtem a lista de extensões
    if Empty(aFileType) .And. At(',',::cFileType)
        aFileType := StrTokArr(::cFileType,',')
    endif
    if Empty(aFileType) .And. At(';',::cFileType)
        aFileType := StrTokArr(::cFileType,';')
    endif
    if Empty(aFileType)
        aAdd(aFileType, ::cFileType)
    endif
    ::cFileType := ""

    // Obtem a lista de arquivos, conforme extensões
    for nI:=1 to Len(aFileType)

        cFileType := aFileType[nI]

        aDirFiles := Directory(::cDirectory+cFileType,,,.F.)

        for nJ:=1 to Len(aDirFiles)

            cFileName := aDirFiles[nJ,1]

            aAdd(aFiles, ::cDirectory + cFileName)

        next nJ

    next nI

    // Envia e-mail
    for nI:=1 to Len(aEmail)

        cTO := AllTrim(aEmail[nI])

        lResult := U_GwSendMail(::cSubject, ::cBody, cTO, ::cEmailCC, ::cEmailCCO, aFiles, @_cRetError)

        if !lResult
            exit
        endif

    next nI

    ::cError := allTrim(_cRetError)
    ::lError := iif(empty(::cError), .F., .T.)

Return lResult
