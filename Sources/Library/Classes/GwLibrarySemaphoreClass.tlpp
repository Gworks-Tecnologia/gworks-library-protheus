#include "tlpp-core.th"

using namespace Gworks.Library.Utils
using namespace Gworks.library.Functions

namespace Gworks.Library.Classes

/*/{Protheus.doc} GwSemaphore
Classe de controle de semáforo.
@type class
@version 12.1.2410
@author Gworks - Giovani
@since 7/2/2025
/*/
Class GwSemaphore

    Public Data cLock as character
    Public Data lLockByCompany as logical
    Public Data lLockByBranch as logical
    Public Data lLockResult as logical

    // Métodos construtores da classe
    Public Method New() Constructor

    // Métodos públicos da classe
    Public Method Clear()
    Public Method ControlByCompany( lEnable as logical )
    Public Method ControlByBranch( lEnable as logical )
    Public Method Lock( cLock as character ) as logical
    Public Method UnLock() as logical

    // Métodos privados da classe
    Private Method Init()

EndClass

/*/{Protheus.doc} GwSemaphore::New
Método construtores da classe.
@type method
@version 12.1.2410
@author Gworks - Giovani
@since 7/2/2025
@return variant, Retorna a instância da classe.
/*/
Method New() Class GwSemaphore

    ::Init()

Return Self

/*/{Protheus.doc} GwSemaphore::Init
Método de incialização da classe.
@type method
@version 12.1.2410
@author Gworks - Giovani
@since 7/2/2025
/*/
Method Init() Class GwSemaphore

    ::cLock := ""
    ::lLockByCompany := .T.
    ::lLockByBranch := .F.

Return

/*/{Protheus.doc} GwSemaphore::Clear
Limpa o controle semafórico.
@type method
@version 12.1.2410
@author Gworks - Giovani
@since 7/2/2025
/*/
Method Clear() Class GwSemaphore

    ::cLock := ""

Return

/*/{Protheus.doc} GwSemaphore::ControlByCompany
Lida/Desliga o controle semafórico por grupo de empresa.
@type method
@version 12.1.2410
@author Gworks - Giovani
@since 7/7/2025
@param lEnable, logical, Se .T. (true) indica se o controle deve ser habilitado e .F. (false) desabilita.
/*/
Method ControlByCompany( lEnable as logical ) Class GwSemaphore

    if( lEnable )
        ::lLockByCompany := .T.
        ::lLockByBranch := .F.
    else
        ::lLockByCompany := .F.
        ::lLockByBranch := .F.
    endif

Return

/*/{Protheus.doc} GwSemaphore::ControlByBranch
Lida/Desliga o controle semafórico por filial.
@type method
@version 12.1.2410
@author Gworks - Giovani
@since 7/7/2025
@param lEnable, logical, Se .T. (true) indica se o controle deve ser habilitado e .F. (false) desabilita.
/*/
Method ControlByBranch( lEnable as logical ) Class GwSemaphore

    if( lEnable )
        ::lLockByCompany := .T.
        ::lLockByBranch := .T.
    else
        ::lLockByCompany := .T.
        ::lLockByBranch := .F.
    endif

Return

/*/{Protheus.doc} GwSemaphore::Lock
Aloca no controle semafórico o conteúdo especificado pelo atributo cLock.
@type method
@version 12.1.2410
@author Gworks - Giovani
@since 7/2/2025
@param cLock, character, Conteúdo a ser alocado no controle semafórico.
/*/
Method Lock( cLock as character ) as logical Class GwSemaphore

    ::cLock := cLock

    ::lLockResult := LockByName( ::cLock, ::lLockByCompany, ::lLockByBranch )

Return ::lLockResult

/*/{Protheus.doc} GwSemaphore::UnLock
Desaloca do controle semafórico o conteúdo especificado pelo método ::Lock().
@type method
@version 12.1.2410
@author Gworks - Giovani Soares
@since 7/2/2025
/*/
Method UnLock() as logical Class GwSemaphore

    Local lResult as logical

    if !empty(::cLock)

        UnLockByName( ::cLock, ::lLockByCompany, ::lLockByBranch )

        lResult := .T.

    else

        lResult := .F.

    endif

Return lResult
