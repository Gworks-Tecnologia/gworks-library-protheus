#include "TOTVS.ch"
#include "MsObject.ch"

#define CRLF chr(13)+chr(10)
#define CRLF2 CRLF+CRLF

using namespace Gworks.Library.Utils
using namespace Gworks.library.Functions

namespace Gworks.Library.Classes

Class GwKeyValue

    Public Data aKeyValue as array default {}

    Public Method New() Constructor
    Public Method Add( cKey as character, xValue as variant )
    Public Method Length() as numeric
    Public Method HasName( cName as character ) as logical
    Public Method HasChildren() as logical
    Public Method GetChildren() as object
    Public Method GetNameByPosition( nPos as numeric ) as character
    Public Method GetAllValues() as array
    Public Method GetValueTypeByPosition( nPos as numeric ) as character
    Public Method GetValueByPosition( nPos as numeric ) as variant
    Public Method GetValueByKey( xSearch as variant ) as variant

EndClass


Method New() Class GwKeyValue

    Self:aKeyValue := {}

Return Self


Method Add( cKey as character, xValue as variant ) Class GwKeyValue

    aAdd(::aKeyValue, { cKey, xValue })

Return


Method Length() as numeric Class GwKeyValue

    Local nResult := 0

    nResult := Len(::aKeyValue)

Return nResult


Method HasName( cName as character ) as logical Class GwKeyValue

    Local nPos    := 0   as numeric
    Local lResult := .F. as logical

    Default cName := ""

    nPos := aScan(::aKeyValue, {|x| x[1] == xSearch })

    if nPos > 0
        lResult := .T.
    endif

Return lResult


Method HasChildren() as logical Class GwKeyValue

    Local lResult := .F. as logical

    if ( Len(::aKeyValue) == 1 .And. ;
         ValType(::aKeyValue[1,2]) == "O" .And. ;
         AllTrim(Upper(GetClassName(::aKeyValue[1,2]))) == "GWORKS.LIBRARY.CLASSES.GWKEYVALUE" )

        lResult := .T.

    endif

Return lResult


Method GetChildren() as object Class GwKeyValue

    Local oResult as object

    if ::HasChildren()
        oResult := ::aKeyValue[1,2]
    endif

Return oResult


Method GetNameByPosition( nPos as numeric ) as character Class GwKeyValue // TODO: testar...

    Local cResult := "" as character

    Default nPos := 0

    if nPos > 0
        cResult := ::aKeyValue[nPos,1]
    endif

Return cResult


Method GetAllValues() as array Class GwKeyValue

    Local aKeyValue := {} as array

    aKeyValue := aClone(::aKeyValue)

Return aKeyValue


Method GetValueTypeByPosition( nPos as numeric ) as character Class GwKeyValue

    Local xValue  := nil as variant
    Local cResult := ""  as character

    xValue := ::GetValueByPosition(nPos)

    cResult := ValType(xValue)

Return cResult


Method GetValueByPosition( nPos as numeric ) as variant Class GwKeyValue // TODO: testar...

    Local xResult := nil as variant

    Default nPos := 0

    if nPos > 0
        xResult := ::aKeyValue[nPos,2]
    endif

Return xResult


Method GetValueByKey( xSearch as variant ) as variant Class GwKeyValue

    Local nPos := 0   as numeric
    Local xResult := nil as variant

    Default xSearch := nil

    if Empty(xSearch)
        UserException("GwKeyValue:GetValueByKey - Parâmetro xSearch inválido ou não informado!")
    endif

    nPos := aScan(::aKeyValue, {|x| x[1] == xSearch })

    if nPos > 0
        xResult := ::aKeyValue[nPos,2]
    endif

Return xResult
