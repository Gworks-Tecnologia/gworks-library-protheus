#include "TOTVS.ch"
#include "MsObject.ch"
#include "TLPP-Core.th"
#include "TLPP-Object.th"

using namespace Gworks.Library.Classes

namespace Gworks.Business.Shared.Entities

Class GwProduto From GwDataAccess

    Public Method New()
    Public Method ProcurarPorProdutoId( cProdutoId as character ) as logical

EndClass

/*/{Protheus.doc} GwProduto::New
Construtor da classe.
@type method
@version 12.1.33
@author Gworks - Giovani
@since 3/27/2023
@return object, Instância do objeto.
/*/
Method New() Class GwProduto

    // Inicializa atributos da classe herdada GwDataAccess
    // Obs.: Sem ao menos referenciar um atributo da classe herdada,
    //       pode ser que o interpretador gere erro em herança da heranaça,
    //       ou seja, quando uma classe for herdada mais de uma vez.
    ::cAlias := ""

    // Atributos extendidos a classes herdadas
    ::cDescription := "Produto"
    ::cSourceModel := ""
    ::cSourceView  := ""
    ::cSourceMenu  := ""

    _Super:New("SB1")

    ::oError:cFunName := "GwProduto"

Return Self

/*/{Protheus.doc} GwProduto::ProcurarPorProdutoId( cProdutoId as character ) as logical
Posiciona a entidade GwProduto conforme identificador (campo B1_COD).
@type method
@version 12.1.33
@author Gworks - Giovani
@since 3/29/2023
@return logical, Retorna .T. para posicionamento realizado com sucesso e .F. falha.
/*/
Method ProcurarPorProdutoId( cProdutoId as character ) as logical Class GwProduto

    Local lResult := .F. as logical

    Default cProdutoId := nil

    if Valtype(cProdutoId) == "U"
        UserException("GwProduto:ProcurarPorProdutoId - Parâmetro cProdutoId não informado ou inválido!")
    endif

    if !Empty(cProdutoId)
        _Super:SaveArea()
        _Super:SelectArea()
        _Super:SetOrder('B1_FILIAL+B1_COD')
        _Super:GoTop()
        if _Super:Seek(cProdutoId)
            lResult := .T.
        endif
    endif

Return lResult

