#include "TOTVS.ch"
#include "FWMVCDEF.ch"
#include "MsObject.ch"
#include "TLPP-Core.th"
#include "TLPP-Object.th"

using namespace Gworks.Library.Classes

namespace Gworks.Business.Estoque.Entities

Class GwSolicitacaoTransferenciaItem From GwDataAccess

    Public Method New()
    Public Method ProcurarPorSolicitacaoId( cSolicitacaoId as character ) as logical

EndClass

/*/{Protheus.doc} GwSolicitacaoTransferenciaItem::New
Construtor da classe.
@type method
@version 12.1.33
@author Gworks - Giovani
@since 3/27/2023
@return object, Instância do objeto.
/*/
Method New() Class GwSolicitacaoTransferenciaItem

    // Inicializa atributos da classe herdada GwDataAccess
    // Obs.: Sem ao menos referenciar um atributo da classe herdada,
    //       pode ser que o interpretador gere erro em herança da heranaça,
    //       ou seja, quando uma classe for herdada mais de uma vez.
    ::cAlias := ""

    // Atributos extendidos a classes herdadas
    ::cDescription := "Solicitação Transf. Item"
    ::cSourceModel := ""
    ::cSourceView  := ""
    ::cSourceMenu  := ""

    _Super:New("NNT")

    ::oError:cFunName := "GwSolicitacaoTransferenciaItem"

Return Self

/*/{Protheus.doc} GwSolicitacaoTransferenciaItem::ProcurarPorSolicitacaoId( cSolicitacaoId as character ) as logical
Posiciona a entidade GwSolicitacaoTransferenciaItem conforme identificador (campo ZK1_CODFLX).
@type method
@version 12.1.33
@author Gworks - Giovani
@since 3/29/2023
@return logical, Retorna .T. para posicionamento realizado com sucesso e .F. falha.
/*/
Method ProcurarPorSolicitacaoId( cSolicitacaoId as character ) as logical Class GwSolicitacaoTransferenciaItem

    Local lResult := .F. as logical

    Default cSolicitacaoId := nil

    if Valtype(cSolicitacaoId) == "U"
        UserException("Usuario:ProcurarPorSolicitacaoId - Parâmetro cSolicitacaoId não informado ou inválido!")
    endif

    if !Empty(cSolicitacaoId)
        _Super:SaveArea()
        _Super:SelectArea()
        _Super:SetOrder('NNT_FILIAL+NNT_COD+NNT_FILORI+NNT_PROD+NNT_LOCAL+NNT_LOCALI+NNT_NSERIE+NNT_LOTECT+NNT_NUMLOT+NNT_FILDES+NNT_PRODD+NNT_LOCLD+NNT_LOCDES+NNT_LOTED')
        _Super:GoTop()
        if _Super:Seek(cSolicitacaoId)
            lResult := .T.
        endif
    endif

Return lResult
